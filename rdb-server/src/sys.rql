type DeploymentBasicInfoMap = map {
  id: string,
  create_time: int64,
  description: string,
};

type DeploymentFullMap = map {
  id: string,
  description: string,
  `schema`: string,
  plan: bytes,
  create_time: int64,
};

type NamespaceMap = map {
  id: string,
  create_time: int64,
};

export graph add_namespace(root: schema, namespace_id: string, create_time: int64): bool {
  ns = root.system.namespaces;
  if is_present $ point_get ns namespace_id {
    r1 = false;
  } else {
    s_insert root.system.namespaces $
      build_table(Namespace) $
      m_insert(id) namespace_id $
      m_insert(deployments) empty_set<Deployment> $
      m_insert(query_scripts) empty_set<QueryScript> $
      m_insert(create_time) create_time $
      create_map;
    r2 = true;
  }
  return select r1 r2;
}

export graph add_or_update_deployment(root: schema, namespace_id: string, deployment: DeploymentFullMap): bool {
  ns = point_get root.system.namespaces namespace_id;
  if !is_present ns {
    r1 = false;
  } else {
    s_insert ns.deployments $ build_table(Deployment) deployment;
    r2 = true;
  }
  return select r1 r2;
}

export graph get_deployment(root: schema, namespace_id: string, deployment_id: string): DeploymentFullMap {
  ns = point_get root.system.namespaces namespace_id;
  if !is_present ns {
    r1 = null<DeploymentFullMap>;
  } else {
    depl = point_get ns.deployments deployment_id;
    if !is_present depl {
      r2 = null<DeploymentFullMap>;
    } else {
      r3 = m_insert(id) depl.id $
        m_insert(create_time) depl.create_time $
        m_insert(description) depl.description $
        m_insert(`schema`) depl.`schema` $
        m_insert(plan) depl.plan $
        create_map;
    }
  }
  return select r1 $ select r2 r3;
}

export graph list_namespaces(root: schema): list<NamespaceMap> {
  return reduce(fold_namespaces) create_map create_list(NamespaceMap) root.system.namespaces;
}

graph fold_namespaces(_unused: map{}, current: list<NamespaceMap>, item: Namespace): list<NamespaceMap> {
  return (
    m_insert(id) item.id $
      m_insert(create_time) item.create_time $
      create_map
  ) : current;
}

export graph list_deployment(root: schema, namespace_id: string): list<DeploymentBasicInfoMap> {
  ns = point_get root.system.namespaces namespace_id;
  if !is_present ns {
    r1 = null<list<DeploymentBasicInfoMap>>;
  } else {
    r2 = reduce(fold_deployments) create_map create_list(DeploymentBasicInfoMap) ns.deployments;
  }
  return select r1 r2;
}

graph fold_deployments(_unused: map{}, current: list<DeploymentBasicInfoMap>, item: Deployment): list<DeploymentBasicInfoMap> {
  return (
    m_insert(id) item.id $
      m_insert(create_time) item.create_time $
      m_insert(description) item.description $
      create_map
  ) : current;
}

export graph delete_namespace(root: schema, namespace_id: string): bool {
  ns = root.system.namespaces;
  if is_present $ point_get ns namespace_id {
    s_delete ns namespace_id;
    r1 = true;
  } else {
    r2 = false;
  }
  return select r1 r2;
}
