use super::ast::*;
use super::TwAsmError;
use super::state::State;
use lalrpop_util::ParseError;
use bumpalo::collections::vec::Vec as Bvec;
use crate::schema::compile::PrimitiveType;

grammar(state: &mut State<'input>);

extern {
  type Error = TwAsmError;
}

pub Root: Root<'input> = {
  Comment* <items:Item*> => Root {
    graphs: Bvec::from_iter_in(items.iter().filter_map(|x| match x {
      Item::Graph(x) => Some(*x),
      _ => None,
    }), &state.alloc),
    type_aliases: Bvec::from_iter_in(items.iter().filter_map(|x| match x {
      Item::TypeAlias(x) => Some(*x),
      _ => None,
    }), &state.alloc),
  }
}

Item: Item<'input> = {
  <g:Graph> => Item::Graph(state.alloc.alloc(g)),
  <t:TypeAlias> => Item::TypeAlias(state.alloc.alloc(t)),
}

TypeAlias: TypeAlias<'input> = {
  Token<"type"> <name:Identifier> Token<"="> <ty:Type> Token<";"> => TypeAlias { name, ty },
}

Graph: Graph<'input> = {
  <exp:Token<"export">?> Token<"graph"> <name:Identifier>
    Token<"("> <params:ZeroOrMore<(Identifier (":" <Type>)?), ",">> Token<")">
    <return_type:(Token<":"> <Type>)?>
    Token<"{"> <stmts:(@L Stmt)*> Token<"}"> => Graph {
      name,
      exported: exp.is_some(),
      params: Bvec::from_iter_in(params.into_iter().map(|x| (x.0, x.1)), &state.alloc),
      return_type,
      stmts: Bvec::from_iter_in(stmts.into_iter().map(|x| Stmt {
        location: x.0,
        kind: x.1,
      }), &state.alloc)
    }
}

Type: Type<'input> = {
  Token<"schema"> => Type::Schema,
  Token<"int64"> => Type::Primitive(PrimitiveType::Int64),
  Token<"string"> => Type::Primitive(PrimitiveType::String),
  Token<"bytes"> => Type::Primitive(PrimitiveType::Bytes),
  Token<"bool"> => Type::Bool,
  Token<"set"> Token<"<"> <ty:Type> Token<">"> => Type::Set(state.alloc.alloc(ty)),
  Token<"list"> Token<"<"> <ty:Type> Token<">"> => Type::List(state.alloc.alloc(ty)),
  Token<"map"> Token<"{"> <members:ZeroOrMore<(Identifier Token<":"> Type), Token<",">>> Token<"}"> => Type::Map(Bvec::from_iter_in(
    members.into_iter().map(|x| (x.0, x.2)),
    &state.alloc
  )),
  <name:Identifier> <params:(Token<"<"> <ZeroOrMore<Type, Token<",">>> Token<">">)?> => Type::Table {
    name,
    params: Bvec::from_iter_in(params.unwrap_or_default().into_iter(), &state.alloc),
  }
}

Stmt: StmtKind<'input> = {
  <name:Identifier> Token<"="> <value:Expr> Token<";"> => StmtKind::Node {
    name: Some(name),
    value,
  },
  Token<"return"> <value:Expr> Token<";"> => StmtKind::Return {
    value,
  },
  <value:Expr> Token<";"> => StmtKind::Node {
    name: None,
    value,
  },
  Token<"if"> <precondition:Expr>
    Token<"{"> <if_body:StmtList> Token<"}">
    <else_body:(Token<"else"> Token<"{"> <StmtList> Token<"}">)?> => StmtKind::If {
    precondition,
    if_body,
    else_body,
  }
}

StmtList: Bvec<'input, Stmt<'input>> = {
  <stmts:(@L Stmt)*> => Bvec::from_iter_in(stmts.into_iter().map(|x| Stmt {
    location: x.0,
    kind: x.1,
  }), &state.alloc),
}

ExprRef: &'input Expr<'input> = {
  <e:Expr> => state.alloc.alloc(e),
}

Expr: Expr<'input> = {
  <location_start:@L> <kind:ExprKind> <location_end:@R> => Expr { location_start, location_end, kind },
  ExprL2,
}

ExprKind: ExprKind<'input> = {
  <x:ExprRef> Token<"&&"> <y:ExprL2Ref> => ExprKind::And(x, y),
  <x:ExprRef> Token<"||"> <y:ExprL2Ref> => ExprKind::Or(x, y),
}

ExprL2Ref: &'input Expr<'input> = {
  <e:ExprL2> => state.alloc.alloc(e),
}

ExprL2: Expr<'input> = {
  <location_start:@L> <kind:ExprKindL2> <location_end:@R> => Expr { location_start, location_end, kind },
  ExprL3,
}

ExprKindL2: ExprKind<'input> = {
  <x:ExprL2Ref> Token<"=="> <y:ExprL3Ref> => ExprKind::Eq(x, y),
  <x:ExprL2Ref> Token<"!="> <y:ExprL3Ref> => ExprKind::Ne(x, y),
}

ExprL3Ref: &'input Expr<'input> = {
  <e:ExprL3> => state.alloc.alloc(e),
}

ExprL3: Expr<'input> = {
  <location_start:@L> <kind:ExprKindL3> <location_end:@R> => Expr { location_start, location_end, kind },
  ExprL3Right,
}

ExprKindL3: ExprKind<'input> = {
  <x:ExprL3Ref> Token<"+"> <y:ExprL3RightRef> => ExprKind::Add(x, y),
  <x:ExprL3Ref> Token<"-"> <y:ExprL3RightRef> => ExprKind::Sub(x, y),
  <x:ExprL3Ref> Token<"??"> <y:ExprL3RightRef> => ExprKind::OrElse(x, y),
}

ExprL3RightRef: &'input Expr<'input> = {
  <e:ExprL3Right> => state.alloc.alloc(e),
}

ExprL3Right: Expr<'input> = {
  <location_start:@L> <kind:ExprKindL3Right> <location_end:@R> => Expr { location_start, location_end, kind },
  ExprL4,
}

ExprKindL3Right: ExprKind<'input> = {
  <x:ExprL4Ref> Token<":"> <y:ExprL3RightRef> => ExprKind::Prepend(x, y),
}

ExprL4Ref: &'input Expr<'input> = {
  <e:ExprL4> => state.alloc.alloc(e),
}

ExprL4: Expr<'input> = {
  <location_start:@L> <kind:ExprKindL4> <location_end:@R> => Expr { location_start, location_end, kind },
  ExprL5,
}

TrailingExprRef: &'input Expr<'input> = {
  <e:ExprL5Ref> => e,
  Token<"$"> <e:ExprL4Ref> => e,
}

ExprKindL4: ExprKind<'input> = {
  Token<"build_table"> Token<"("> <x:Type> Token<")"> <y:TrailingExprRef> => ExprKind::BuildTable(x, y),
  Token<"build_set"> <x:TrailingExprRef> => ExprKind::BuildSet(x),
  Token<"point_get"> <x:ExprL5Ref> <y:TrailingExprRef> => ExprKind::GetSetElement(x, y),
  Token<"m_insert"> Token<"("> <x:Identifier> Token<")"> <y:ExprL5Ref> <z:TrailingExprRef> => ExprKind::InsertIntoMap(x, y, z),
  Token<"t_insert"> Token<"("> <x:Identifier> Token<")"> <y:ExprL5Ref> <z:TrailingExprRef> => ExprKind::InsertIntoTable(x, y, z),
  Token<"s_insert"> <y:ExprL5Ref> <z:TrailingExprRef> => ExprKind::InsertIntoSet(y, z),
  Token<"m_delete"> Token<"("> <x:Identifier> Token<")"> <y:TrailingExprRef> => ExprKind::DeleteFromMap(x, y),
  Token<"t_delete"> Token<"("> <x:Identifier> Token<")"> <y:TrailingExprRef> => ExprKind::DeleteFromTable(x, y),
  Token<"s_delete"> <y:ExprL5Ref> <z:TrailingExprRef> => ExprKind::DeleteFromSet(y, z),
  Token<"select"> <x:ExprL5Ref> <y:TrailingExprRef> => ExprKind::Select(x, y),
  Token<"!"> <x:ExprL4Ref> => ExprKind::Not(x),
  Token<"is_present"> <x:TrailingExprRef> => ExprKind::IsPresent(x),
  Token<"is_null"> <x:TrailingExprRef> => ExprKind::IsNull(x),
  Token<"call"> Token<"("> <name:Identifier> Token<")"> Token<"["> <params:ZeroOrMore<Expr, ",">> Token<"]"> => ExprKind::Call(name, Bvec::from_iter_in(params.into_iter(), &state.alloc)),
  Token<"reduce"> Token<"("> <name:Identifier> Token<")">
    <subgraph_param:ExprL5Ref> <reduce_init:ExprL5Ref> <list_or_set:TrailingExprRef> => ExprKind::Reduce(
      name, subgraph_param, reduce_init, list_or_set,
    ),
  Token<"pop"> <x:TrailingExprRef> => ExprKind::Pop(x),
  Token<"head"> <x:TrailingExprRef> => ExprKind::Head(x),
}

ExprL5Ref: &'input Expr<'input> = {
  <e:ExprL5> => state.alloc.alloc(e),
}

ExprL5: Expr<'input> = {
  <location_start:@L> <kind:ExprKindL5> <location_end:@R> => Expr { location_start, location_end, kind },
  Token<"("> <e:Expr> Token<")"> => e,
}

ExprKindL5: ExprKind<'input> = {
  <x:Literal> => ExprKind::LoadConst(x),
  Token<"create_map"> => ExprKind::CreateMap,
  Token<"create_list"> Token<"("> <ty:Type> Token<")"> => ExprKind::CreateList(ty),
  <x:Identifier> => ExprKind::Node(x),
  <y:ExprL5Ref> Token<"."> <x:Identifier> => ExprKind::GetField(x, y),
}

Identifier: &'input str = {
  <s:Token<r"[a-zA-Z_][0-9a-zA-Z_]*">> => state.resolve_str(s),
}

Literal: Literal<'input> = {
  <s:Token<r"-?[0-9]+">> =>? s.parse().map(Literal::Integer).map_err(|_| ParseError::User {
    error: TwAsmError::InvalidLiteral,
  }),
  <s:Token<r"0x[0-9a-fA-F]+">> =>? i64::from_str_radix(s.strip_prefix("0x").unwrap(), 16).map(Literal::Integer).map_err(|_| ParseError::User {
    error: TwAsmError::InvalidLiteral,
  }),
  <s:Token<r"0o[0-9a-fA-F]+">> =>? i64::from_str_radix(s.strip_prefix("0o").unwrap(), 8).map(Literal::Integer).map_err(|_| ParseError::User {
    error: TwAsmError::InvalidLiteral,
  }),
  <s:Token<r"0b[0-9a-fA-F]+">> =>? i64::from_str_radix(s.strip_prefix("0b").unwrap(), 2).map(Literal::Integer).map_err(|_| ParseError::User {
    error: TwAsmError::InvalidLiteral,
  }),
  <s:StringLit> => Literal::String(state.resolve_str(&s)),
  <s:HexBytesLit> => Literal::HexBytes(s),
  Token<"null"> Token<"<"> <ty:Type> Token<">"> => Literal::Null(ty),
  Token<"true"> => Literal::Bool(true),
  Token<"false"> => Literal::Bool(false),
  Token<"empty_set"> Token<"<"> <member_ty:Type> Token<">"> => Literal::EmptySet(member_ty),
}

StringLit: String = {
  <s:Token<r#""(\\.|[^"])*""#>> =>? serde_json::from_str::<String>(s)
    .map_err(|_| ParseError::User {
      error: TwAsmError::InvalidLiteral,
    }),
}

HexBytesLit: &'input [u8] = {
  <s:Token<r#"h"([0-9a-fA-F][0-9a-fA-F])*""#>> =>? serde_json::from_str::<String>(s.strip_prefix("h\"").unwrap().strip_suffix("\"").unwrap())
    .map_err(|_| ParseError::User {
      error: TwAsmError::InvalidLiteral,
    })
    .and_then(|x| hex::decode(&x)
      .map_err(|_| ParseError::User {
        error: TwAsmError::InvalidLiteral,
      })
      .map(|x| state.alloc.alloc_slice_copy(&x) as &[u8])
    ),
}

ZeroOrMore<T, Delim>: Vec<T> = {
  <x:OneOrMore<T, Delim>?> => x.unwrap_or_default()
}

OneOrMore<T, Delim>: Vec<T> = {
  <i1: T> <i2:(Delim T)*> Delim? => {
    let mut items = vec![i1];
    items.extend(i2.into_iter().map(|e| e.1));
    items
  }
}

Token<I>: I = {
  <s:I> => s,
}

Comment: () = {
  r"//[^\n\r]*[\n\r]*" => { },
  r"/\*([^\*]*\*+[^\*/])*([^\*]*\*+|[^\*])*\*/" => { },
}
